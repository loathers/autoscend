// functions that deal with acquiring items. via buying or pulling

boolean haveAny(boolean[item] array)
{
	foreach thing in array
	{
		if(item_amount(thing) > 0)
		{
			return true;
		}
	}
	return false;
}

boolean acquireOrPull(item it)
{
	//this function is for when you want to make sure you have 1 of an item
	//if you have one it returns true. if you don't it will craft one. if it can't it will pull it.
	
	if(possessEquipment(it)) return true;
	if(item_amount(it) > 0)  return true;
	if(retrieve_item(1, it)) return true;
	if(canPull(it))
	{
		if(pullXWhenHaveY(it, 1, 0)) return true;
	}
	
	//special handling via pulling 1 ingredient to craft the item desired
	if($items[asteroid belt, meteorb, shooting morning star, meteorite guard, meteortarboard, meteorthopedic shoes] contains it)
	{
		if(canPull($item[metal meteoroid]))
		{
			if(pullXWhenHaveY($item[metal meteoroid], 1, 0))
			{
				if(retrieve_item(1, it)) return true;
			}
		}
	}
	
	return false;
}

boolean canPull(item it, boolean historical)
{
	if(in_hardcore())
	{
		return false;
	}
	if(in_lol())
	{
		// kol states "Only food, booze, potions, combat and usable items may be pulled on this path."
		if(it.fullness == 0 && it.inebriety == 0 && !it.potion && !it.combat && !it.usable)
		{
			return false;
		}
	}
	if(it == $item[none])
	{
		return false;
	}
	if(!is_unrestricted(it))
	{
		return false;
	}
	if(pulls_remaining() == 0)
	{
		return false;
	}
	if(pulledToday(it))
	{
		return false;
	}
	
	if(storage_amount(it) > 0)
	{
		return true;	//we have it in storage so we can pull it
	}
	else if(!is_tradeable(it))
	{
		return false;	//we do not have it in storage and we can not trade for it. gifts currently not handled
	}

	if(!auto_is_valid(it))
	{
		return false;
	}

	int meat = my_storage_meat();
	if(can_interact())
	{
		meat = max(meat, my_meat() - 5000);
	}
	int curPrice = historical_price(it);
	if(!historical)
	{
		curPrice = auto_mall_price(it);
	}
	if(curPrice < 1)
	{
		return false;	//a 0 or a -1 indicates the item is not available.
	}
	if(curPrice > get_property("autoBuyPriceLimit").to_int())
	{
		return false;
	}
	else if(curPrice < meat)
	{
		return true;
	}
	
	return false;
}

boolean canPull(item it)
{
	return canPull(it, false);
}

boolean pulledToday(item it)
{
	//autoscend property "auto_pulls" tracks pulls made by the script as "(" + my_daycount() + ":" + it
	//kolmafia property "_roninStoragePulls" tracks all pulls made with kolmafia today since 2022 changed to daily limit of one pull for each item
	string [int] allPulls = split_string(get_property("_roninStoragePulls"),",");
	foreach i in allPulls
	{
		if(allPulls[i].to_int() == it.to_int())
		{
			return true;
		}
	}
	return false;
}

int auto_mall_price(item it)
{
	if(isSpeakeasyDrink(it))
	{
		return -1;	//speakeasy drinks are marked as tradeable but cannot be acquired as a physical item to trade.
	}
	if(available_choice_options() contains 0 || available_choice_options() contains 1)	//we are in a choice adventure.
	{
		//mafia returns -1 if we check mall_price() while in a choice adv. better to use historical price even if it is outdated
		return historical_price(it);
	}
	if(is_tradeable(it))
	{
		int retval;
		string it_type = item_type(it);
		if(it_type == "food" || it_type == "booze")
		{
			//autoscend does Bulk cache mall prices for food,booze,hprestore,mprestore so that when asking for mall_price it gets a cached mafia session price
			//directly ask for historical_price here if it exists because if mafia session has to be restarted mafia will do another search despite recent price
			//hprestore and mprestore types corresponding with mall_prices search categories are not available
			retval = historical_price(it);
			if(retval == 0)
			{
				retval = mall_price(it);
			}
		}
		else
		{
			retval = mall_price(it);
		}
		if(retval == -1)
		{
			//0 could be due to item not being tradeable.
			//-1 could be due to tradeable item not found in the mall. Or due to an IO error during lookup
			//-1 is non trivial to fix due to mafia anti abuse code
			//historical price can never be -1. only 0 or a positive number
			//just use the historical price. It will be good enough. it never returns -1. and if it returns 0 it is because this mafia install never happened to look up that item before. which suggests an extreme edge case or that the item is really unavailable
			return historical_price(it);
		}
		return retval;
	}
	return -1;
}

boolean pullXWhenHaveYCasual(item it, int howMany, int whenHave)
{
	//we are either in a casual run. or in postronin. either way pull becomes mallbuy
	if(!can_interact())
	{
		return false;
	}
	if(it == $item[none])
	{
		return false;
	}
	if(!auto_is_valid(it))
	{
		return false;
	}
	if((item_amount(it) + equipped_amount(it)) != whenHave)
	{
		return false;
	}
	if(inAftercore())
	{
		take_storage(storage_amount(it), it);
	}
	int maxprice = get_property("autoBuyPriceLimit").to_int();
	while((item_amount(it) < howMany) && (auto_mall_price(it) < maxprice))
	{
		if(auto_mall_price(it) > my_meat())
		{
			abort("Don't have enough meat to restock, big sad");
		}
		if(buy(1, it, maxprice) == 0)
		{
			auto_log_info("Price of " + it + " exceeded expected mall price of " + maxprice + ".", "blue");
			return false;
		}
	}
	if(item_amount(it) < howMany)
	{
		if(auto_mall_price(it) >= maxprice)
		{
			auto_log_info("Price of " + it + " exceeded expected mall price of " + maxprice + ".", "blue");
		}
		return false;
	}
	return true;
}

boolean pullXWhenHaveY(item it, int howMany, int whenHave)
{
	if(can_interact())
	{
		return pullXWhenHaveYCasual(it, howMany, whenHave);
	}
	if(!canPull(it))
	{
		return false;
	}
	if((item_amount(it) + equipped_amount(it)) == whenHave)
	{
		int lastStorage = storage_amount(it);
		while(storage_amount(it) < howMany)
		{
			int oldPrice = historical_price(it) * 1.2;
			int curPrice = auto_mall_price(it);
			int meat = my_storage_meat();
			int priceLimit = get_property("autoBuyPriceLimit").to_int();
			boolean getFromStorage = true;
			if(can_interact() && (meat < curPrice))
			{
				meat = my_meat() - 5000;
				getFromStorage = false;
			}
			if(curPrice >= priceLimit)
			{
				auto_log_warning(it + " is too expensive at " + curPrice + " meat, we're gonna skip buying one in the mall.", "red");
				break;
			}
			if((curPrice <= oldPrice) && (curPrice < priceLimit) && (meat >= curPrice))
			{
				if(getFromStorage)
				{
					buy_using_storage(howMany - storage_amount(it), it, curPrice);
				}
				else
				{
					howMany -= buy(howMany - storage_amount(it), it, curPrice);
				}
			}
			else
			{
				if(curPrice > oldPrice)
				{
					auto_log_warning("Price of " + it + " may have been mall manipulated. Expected to pay at most: " + oldPrice, "red");
				}
				if(my_storage_meat() < curPrice)
				{
					auto_log_warning("Do not have enough meat in Hagnk's to buy " + it + ". Need " + curPrice + " have " + my_storage_meat() + ".", "blue");
				}
			}
			if(lastStorage == storage_amount(it))
			{
				break;
			}
			lastStorage = storage_amount(it);
		}

		if(storage_amount(it) < howMany)
		{
			auto_log_warning("Can not pull what we don't have. Sorry");
			return false;
		}

		auto_log_info("Trying to pull " + howMany + " of " + it, "blue");
		boolean retval = take_storage(howMany, it);
		if(item_amount(it) != (howMany + whenHave))
		{
			auto_log_warning("Failed pulling " + howMany + " of " + it, "red");
		}
		else
		{
			for(int i = 0; i < howMany; ++i)
			{
				handleTracker(it, "auto_pulls");
			}
		}
		return retval;
	}
	return false;
}

boolean pulverizeThing(item it)
{
	if(!have_skill($skill[Pulverize]))
	{
		return false;
	}
	if(item_amount($item[Tenderizing Hammer]) == 0)
	{
		if(my_meat() < npc_price($item[Tenderizing Hammer]))
		{
			return false;
		}
	}

	if(item_amount(it) == 0)
	{
		if(closet_amount(it) == 0)
		{
			return false;
		}
		take_closet(1, it);
	}
	if(item_amount(it) == 0)
	{
		return false;
	}
	cli_execute("pulverize 1 " + it);
	return true;
}

boolean buyableMaintain(item toMaintain, int howMany)
{
	return buyableMaintain(toMaintain, howMany, 0, true);
}

boolean buyableMaintain(item toMaintain, int howMany, int meatMin)
{
	return buyableMaintain(toMaintain, howMany, meatMin, true);
}

boolean buyableMaintain(item toMaintain, int howMany, int meatMin, boolean condition)
{
	if((!condition) || (my_meat() < meatMin) || in_wotsf())
	{
		return false;
	}

	return auto_buyUpTo(howMany, toMaintain);
}

boolean auto_buyUpTo(int num, item it)
{
	if(is_werewolf())
	{
		return false; //can't buy anything as a werewolf
	}
	if(item_amount(it) >= num)
	{
		return true;	//we already have the target amount
	}
	if(($items[Ben-Gal&trade; Balm, Hair Spray] contains it) && !isGeneralStoreAvailable())
	{
		return false;
	}
	if(($items[Blood of the Wereseal, Cheap Wind-Up Clock, Turtle Pheromones] contains it) && !isMusGuildStoreAvailable())
	{
		return false;
	}

	int missing = num - item_amount(it);
	int maxprice = get_property("autoBuyPriceLimit").to_int();
	if(can_interact() && shop_amount(it) > 0 && mall_price(it) < maxprice)	//prefer to buy from yourself
	{
		take_shop(min(missing, shop_amount(it)), it);
		missing = num - item_amount(it);
	}
	if(missing > 0)
	{
		buy(missing, it, maxprice);
		if(item_amount(it) < num)
		{
			auto_log_warning("Could not auto_buyUpTo(" + num + ") of " + it + ". Maxprice: " + maxprice, "red");
		}
	}
	return (item_amount(it) >= num);
}

float npcStoreDiscountMulti()
{
	//calculates a multiplier to be applied to store prices for our current discount for NPC stores.
	//does not bother with sleaze jelly or Post-holiday sale coupon
	
	float retval = 1.0;
	
	if(auto_have_skill($skill[Five Finger Discount]))
	{
		retval -= 0.05;
	}
	if(possessEquipment($item[Travoltan trousers]) && auto_is_valid($item[Travoltan trousers]))
	{
		retval -= 0.05;
	}
	
	return retval;
}

boolean acquireGumItem(item it)
{
	if(!isGeneralStoreAvailable())
	{
		return false;
	}

	if(!($items[Disco Ball, Disco Mask, Helmet Turtle, Hollandaise Helmet, Mariachi Hat, Old Sweatpants, Pasta Spoon, Ravioli Hat, Saucepan, Seal-Clubbing Club, Seal-Skull Helmet, Stolen Accordion, Turtle Totem, Worthless Gewgaw, Worthless Knick-Knack, Worthless Trinket] contains it))
	{
		return false;
	}

	int have = item_amount(it);
	auto_log_info("Gum acquisition of: " + it, "green");
	while((have == item_amount(it)) && (my_meat() >= npc_price($item[Chewing Gum on a String])))
	{
		auto_buyUpTo(1, $item[Chewing Gum on a String]);
		use(1, $item[Chewing Gum on a String]);
	}

	return (have + 1) == item_amount(it);
}

boolean acquireTotem()
{
	//this function checks if you have a valid totem for casting turtle tamer buffs with. Returning true if you do. If you don't, it will attempt to acquire one in a reasonable manner.

	//check if there is a valid totem in inventory or equipped, return true if there is.
	//check the closet from best to worst. If found in closet, uncloset 1 and return true
	
	foreach totem in $items[primitive alien totem, flail of the seven aspects, chelonian morningstar, mace of the tortoise, ouija board\, ouija board, turtle totem]
	{
		if(possessEquipment(totem))
		{
			return true;
		}
		if(0 < closet_amount(totem))
		{
			take_closet(1, totem);
			return true;
		}
	}

	boolean want_totem = false;
	foreach sk in $skills[Empathy of the Newt, Astral Shell, Ghostly Shell, Tenacity of the Snapper, Spiky Shell, Reptilian Fortitude, Jingle Bells, Curiosity of Br\'er Tarrypin]
	{
		if(auto_have_skill(sk))
		{
			want_totem = true;
		}
	}
	if(want_totem)
	{	
		//try fishing in the sewer for a turtle totem
		if(acquireGumItem($item[turtle totem]))
		{
			return true;
		}
	}
	
	//did not get a totem. Give up
	return false;
}

boolean auto_hermit(int amt, item it)
{
	//workaround for this bug https://kolmafia.us/threads/27105/
	if(it != $item[11-Leaf Clover])
	{
		return hermit(amt, it);
	}
	int initial = item_amount(it);
	catch hermit(amt, it);
	return item_amount(it) == initial + amt;
}

boolean acquireHermitItem(item it)
{
	if(!isHermitAvailable())
	{
		return false;
	}
	
	if((item_amount($item[Hermit Permit]) == 0) && (my_meat() >= npc_price($item[Hermit Permit])))
	{
		auto_buyUpTo(1, $item[Hermit Permit]);
	}
	if(item_amount($item[Hermit Permit]) == 0)
	{
		return false;
	}
	if(!($items[Banjo Strings, Catsup, Chisel, Figurine of an Ancient Seal, Hot Buttered Roll, Jaba&ntilde;ero Pepper, Ketchup, Petrified Noodles, Seal Tooth, 11-Leaf Clover, Volleyball, Wooden Figurine] contains it))
	{
		return false;
	}
	if((it == $item[Figurine of an Ancient Seal]) && (my_class() != $class[Seal Clubber]))
	{
		return false;
	}
	if(it == $item[11-leaf clover] && get_property("_cloversPurchased") >= 3) {
		return false;
	}
	if(!isGeneralStoreAvailable())
	{
		return false;
	}
	int have = item_amount(it);
	auto_log_info("Hermit acquisition of: " + it, "green");
	while((have == item_amount(it)) && ((my_meat() >= npc_price($item[Chewing Gum on a String])) || ((item_amount($item[Worthless Trinket]) + item_amount($item[Worthless Gewgaw]) + item_amount($item[Worthless Knick-knack])) > 0)))
	{
		if((item_amount($item[Worthless Trinket]) + item_amount($item[Worthless Gewgaw]) + item_amount($item[Worthless Knick-knack])) > 0)
		{
			if(!auto_hermit(1, it))
			{
				return false;
			}
		}
		else
		{
			if(is_werewolf())
			{
				return false; //can access the hermit, but can't buy chewing gum as a Werewolf
			}
			auto_buyUpTo(1, $item[Chewing Gum on a String]);
			use(1, $item[Chewing Gum on a String]);
		}
	}

	return (have + 1) == item_amount(it);
}

boolean pull_meat(int target)
{
	//pulls meat to reach the desired target amount. preferentially will pull items and autosell them.
	if(my_meat() >= target)
	{
		return true;	//already have target meat
	}
	if(pulls_remaining() < 1)		//hardcore returns 0. casual returns -1. both are < 1
	{
		return false;	//can not pull
	}
	if(in_wotsf())
	{
		return false;	//can not pull meat & autoselling items just donates them
	}
	
	//pull and autosell items
	while(my_meat() < target && pulls_remaining() > 0)
	{
		boolean fail = true;		//if true an item was not pulled and sold this loop
		foreach it in $items[1\,970 carat gold]
		{
			if(fail && storage_amount(it) > 0 && is_unrestricted(it))
			{
				if(pullXWhenHaveY(it, 1, 0) && autosell(1, it))		//pull and sell
				{
					fail = false;
				}
			}
		}
		if(fail) break;
	}
	
	//pull meat directly
	if(my_meat() < target && my_storage_meat() >= target)
	{
		float meat_pulls = target - my_meat();					//how much meat we need to pull. converted to float
		meat_pulls = ceil(meat_pulls / 1000.0);					//how many pulls it will require to get.
		meat_pulls = min(pulls_remaining(), meat_pulls);		//limit by remaining pulls
		int meat_pull_int = meat_pulls * 1000;		//we want to round it up to nearest 1000
		if(meat_pulls > 0)
		{
			cli_execute("pull " +meat_pull_int+ " meat");
		}
	}
	
	return my_meat() >= target;
}

int handlePulls(int day)
{
	if(item_amount($item[Astral Six-Pack]) > 0)
	{
		use(1, $item[Astral Six-Pack]);
	}
	if(item_amount($item[Astral Hot Dog Dinner]) > 0)
	{
		use(1, $item[Astral Hot Dog Dinner]);
	}
	if(item_amount($item[[10882]carton of astral energy drinks]) > 0)
	{
		use(1, $item[[10882]carton of astral energy drinks]);
	}

	if(in_hardcore())
	{
		return 0;
	}

	if(day == 1)
	{
		set_property("lightsOutAutomation", "1");
		# Do starting pulls:
		if((pulls_remaining() != 20) && !in_hardcore() && (my_turncount() > 0))
		{
			auto_log_info("I assume you've handled your pulls yourself... who knows.");
			return 0;
		}
		if(auto_turbo())
		{
			//Pull expensive organ cleansers first if you are running turbo and you own them
			foreach it in $items[Spice Melange, Ultra Mega Sour Ball, Alien plant pod, Alien animal milk]
			{
				if(storage_amount(it) > 0 && auto_is_valid(it) && !pulledToday(it))
				{
					user_confirm("Pulling a " + it + ". If you are ok with this, you have 15 seconds to hit 'Yes'", 15000, false);
					{
						pullXWhenHaveY(it, 1, 0);
					}
				}
			}
			pullXWhenHaveY($item[Dieting Pill], 1, 0);
			//Make sure we have the legendary pizzas if we want to/can consume them so we take full advantage of the dieting pills
			if(!get_property("auto_dontConsumeLegendPizzas").to_boolean())
			{
				foreach it in $items[Pizza of Legend, Calzone of Legend, Deep Dish of Legend]
				{
					if(auto_is_valid(it) && !pulledToday(it))
					{
						pullXWhenHaveY(it, 1, 0);
					}
				}
			}
		}

		// pulls for small path
		auto_SmallPulls();
		// pulls for 11THIAU path
		iluh_pulls();
		// pulls for Avant Guard path
		ag_pulls();

		// generic pulls for any path are below
		if(auto_is_valid($item[etched hourglass]))
		{
			pullXWhenHaveY($item[etched hourglass], 1, 0);
		}
		if(auto_is_valid($item[Infinite BACON Machine]))
		{
			pullXWhenHaveY($item[Infinite BACON Machine], 1, 0);
		}

		// things we would always pull but cannot pull due to LoL usage restrictions
		if(!in_lol())
		{
			if((storage_amount($item[mafia thumb ring]) > 0) && auto_is_valid($item[mafia thumb ring]))
			{
				pullXWhenHaveY($item[mafia thumb ring], 1, 0);
			}
			if((storage_amount($item[can of rain-doh]) > 0) && auto_is_valid($item[Can Of Rain-Doh]) && (pullXWhenHaveY($item[can of Rain-doh], 1, 0)))
			{
				if(item_amount($item[Can of Rain-doh]) > 0)
				{
					use(1, $item[can of Rain-doh]);
					put_closet(1, $item[empty rain-doh can]);
				}
			}
			if(storage_amount($item[Buddy Bjorn]) > 0 && auto_is_valid($item[Buddy Bjorn]) && pathHasFamiliar())
			{
				pullXWhenHaveY($item[Buddy Bjorn], 1, 0);
			}
			if((storage_amount($item[Camp Scout Backpack]) > 0) && !possessEquipment($item[Buddy Bjorn]) && auto_is_valid($item[Camp Scout Backpack]))
			{
				pullXWhenHaveY($item[Camp Scout Backpack], 1, 0);
			}
			if(!possessEquipment($item[Astral Shirt]))
			{
				boolean getPeteShirt = true;
				if(!hasTorso())
				{
					getPeteShirt = false;
				}
				if((my_primestat() == $stat[Muscle]) && get_property("loveTunnelAvailable").to_boolean())
				{
					getPeteShirt = false;
				}
				if(in_glover())
				{
					getPeteShirt = false;
				}
				if(storage_amount($item[Sneaky Pete\'s Leather Jacket]) == 0 && storage_amount($item[Sneaky Pete\'s Leather Jacket (Collar Popped)]) == 0)
				{
					getPeteShirt = false;
				}
				if(getPeteShirt)
				{
					pullXWhenHaveY($item[Sneaky Pete\'s Leather Jacket], 1, 0);
					if(item_amount($item[Sneaky Pete\'s Leather Jacket]) == 0)
					{
						pullXWhenHaveY($item[Sneaky Pete\'s Leather Jacket (Collar Popped)], 1, 0);
					}
					else
					{
						auto_fold($item[Sneaky Pete\'s Leather Jacket (Collar Popped)]);
					}
				}
			}
			if((equipped_item($slot[folder1]) == $item[folder (tranquil landscape)]) && (equipped_item($slot[folder2]) == $item[folder (skull and crossbones)]) && (equipped_item($slot[folder3]) == $item[folder (Jackass Plumber)]) && auto_is_valid(wrap_item($item[Over-The-Shoulder Folder Holder])))
			{
				pullXWhenHaveY($item[over-the-shoulder folder holder], 1, 0);
			}
			if(auto_have_skill($skill[Summon Smithsness]))
			{
				pullXWhenHaveY($item[hand in glove], 1, 0);
			}
			pullLegionKnife();
		}

		// pulls excluding a handful of paths are below
		if(!in_heavyrains() && pathHasFamiliar())
		{	// heavy rains familiars largely need miniature life preserver
			if(!possessEquipment($item[Snow Suit]) && !possessEquipment($item[Astral Pet Sweater]) && auto_is_valid($item[Snow Suit]))
			{
				pullXWhenHaveY($item[snow suit], 1, 0);
			}
			boolean famStatEq = possessEquipment($item[fuzzy polar bear ears]) || possessEquipment($item[miniature goose mask]) || possessEquipment($item[tiny glowing red nose]);
			
			if(!possessEquipment($item[Snow Suit]) && !possessEquipment($item[Filthy Child Leash]) && !possessEquipment($item[Astral Pet Sweater]) &&
			!famStatEq && auto_is_valid($item[Filthy Child Leash]))
			{
				pullXWhenHaveY($item[Filthy Child Leash], 1, 0);
			}
		}
		if(!in_pokefam() && auto_is_valid($item[Replica Bat-oomerang]))
		{	// cannot use combat items in pokefam
			pullXWhenHaveY($item[Replica Bat-oomerang], 1, 0);
		}
		if((my_primestat() == $stat[Muscle]) && !in_wotsf() && !in_heavyrains() && !in_lol())
		{	// no need for shields in WotSF; cannot pull them in LoL
			if(possessEquipment($item[familiar scrapbook]) && auto_is_valid($item[familiar scrapbook]) && my_class() != $class[Turtle Tamer])
			{
				// familiar scrapbook will probably be equipped in preference to Fake Washboard
			}
			else if((closet_amount($item[Fake Washboard]) == 0) && auto_is_valid($item[Fake Washboard]))
			{
				pullXWhenHaveY($item[Fake Washboard], 1, 0);
			}
			if((item_amount($item[Fake Washboard]) == 0) && (closet_amount($item[Fake Washboard]) == 0))
			{
				pullXWhenHaveY($item[numberwang], 1, 0);
			}
			else
			{
				if(get_property("barrelShrineUnlocked").to_boolean())
				{
					put_closet(item_amount($item[Fake Washboard]), $item[Fake Washboard]);
				}
			}
		}
		else
		{
			pullXWhenHaveY($item[Numberwang], 1, 0);
		}
		if(((my_class() == $class[Sauceror]) || (my_class() == $class[Pastamancer])) && !in_wotsf() && !in_lol())
		{	// no need for offhands in WotSF; cannot pull items in LoL
			if((item_amount(wrap_item($item[Deck of Every Card])) == 0) && !auto_have_skill($skill[Summon Smithsness]))
			{
				pullXWhenHaveY($item[Thor\'s Pliers], 1, 0);
			}
			if(auto_is_valid($item[Basaltamander Buckler]))
			{
				pullXWhenHaveY($item[Basaltamander Buckler], 1, 0);
			}
		}

// path specific pulls are below
		if(in_wotsf())
		{
			pullXWhenHaveY($item[Bittycar Meatcar], 1, 0);
		}
		if((in_picky() || !canChangeFamiliar()) && (item_amount(wrap_item($item[Deck of Every Card])) == 0) && (fullness_left() >= 4))
		{
			if((item_amount($item[Boris\'s Key]) == 0) && canEat($item[Boris\'s Key Lime Pie]) && !contains_text(get_property("nsTowerDoorKeysUsed"), $item[Boris\'s Key]))
			{
				pullXWhenHaveY($item[Boris\'s Key Lime Pie], 1, 0);
			}
			if((item_amount($item[Sneaky Pete\'s Key]) == 0) && canEat($item[Sneaky Pete\'s Key Lime Pie]) && !contains_text(get_property("nsTowerDoorKeysUsed"), $item[Sneaky Pete\'s Key]))
			{
				pullXWhenHaveY($item[Sneaky Pete\'s Key Lime Pie], 1, 0);
			}
			if((item_amount($item[Jarlsberg\'s Key]) == 0) && canEat($item[Jarlsberg\'s Key Lime Pie]) && !contains_text(get_property("nsTowerDoorKeysUsed"), $item[Jarlsberg\'s Key]))
			{
				pullXWhenHaveY($item[Jarlsberg\'s Key Lime Pie], 1, 0);
			}
		}
		if(in_picky())
		{
			pullXWhenHaveY($item[gumshoes], 1, 0);
		}
		if(in_pokefam())
		{
			pullXWhenHaveY($item[Ring Of Detect Boring Doors], 1, 0);
			pullXWhenHaveY($item[Pick-O-Matic Lockpicks], 1, 0);
			pullXWhenHaveY($item[Eleven-Foot Pole], 1, 0);
		}
		if(in_darkGyffte())
		{
			auto_log_info("You are a powerful vampire who is doing a softcore run. Turngen is busted in this path, so let's see how much we can get.", "blue");
			pullXWhenHaveY($item[mime army shotglass], 1, 0);
		}
		if(in_lol())
		{	// some items that can be pulled to help accelerate runs
			pullXWhenHaveY($item[portable pantogram], 1, 0);
			pullXWhenHaveY($item[SpinMaster&trade; lathe], 1, 0);
			pullXWhenHaveY($item[Asdon Martin keyfob (on ring)], 1, 0);		
		}
	}
	else if(day == 2)
	{
		if((closet_amount($item[Fake Washboard]) == 1) && get_property("barrelShrineUnlocked").to_boolean())
		{
			take_closet(1, $item[Fake Washboard]);
		}
		// pulls for Avant Guard path
		ag_pulls();
	}

	// do this regardless of day if we still need to complete the bridge.
	if(canPull($item[smut orc keepsake box]) && (lumberCount() + 5 <= bridgeGoal()) && (fastenerCount() + 5 <= bridgeGoal()))
	{
		if(pullXWhenHaveY($item[smut orc keepsake box], 1, 0))
		{
			use(1, $item[smut orc keepsake box]);
		}
	}

	return pulls_remaining();
}

boolean LX_craftAcquireItems()
{
	if((get_property("lastGoofballBuy").to_int() != my_ascensions()) && (internalQuestStatus("questL03Rat") >= 0))
	{
		visit_url("place.php?whichplace=woods");
		auto_log_info("Gotginb Goofballs", "blue");
		visit_url("tavern.php?place=susguy&action=buygoofballs", true);
		put_closet(item_amount($item[Bottle of Goofballs]), $item[Bottle of Goofballs]);
	}

	auto_floundryUse();

	// Snow Berries can be acquired out of standard by using Van Keys from NEP. We need to check to make sure they are usable.
	if(auto_is_valid($item[snow berries]))
	{
		if((item_amount($item[snow berries]) == 3) && (my_daycount() == 1) && get_property("auto_grimstoneFancyOilPainting").to_boolean())
		{
			cli_execute("make 1 snow cleats");
		}

		if((item_amount($item[snow berries]) > 0) && (my_daycount() > 1) && (get_property("chasmBridgeProgress").to_int() >= bridgeGoal()) && (my_level() >= 9))
		{
			visit_url("place.php?whichplace=orc_chasm");
			if(get_property("chasmBridgeProgress").to_int() >= bridgeGoal())
			{
				#if(in_hardcore() && isGuildClass())
				if(isGuildClass())
				{
					if((item_amount($item[Snow Berries]) >= 3) && (item_amount($item[Ice Harvest]) >= 3) && (item_amount($item[Unfinished Ice Sculpture]) == 0))
					{
						cli_execute("make 1 Unfinished Ice Sculpture");
					}
					if((item_amount($item[Snow Berries]) >= 2) && (item_amount($item[Snow Crab]) == 0))
					{
						cli_execute("make 1 Snow Crab");
					}
				}
				#cli_execute("make " + item_amount($item[snow berries]) + " snow cleats");
			}
			else
			{
				abort("Bridge progress came up as >= 30 but is no longer after viewing the page.");
			}
		}
	}

	if(knoll_available() && (item_amount($item[Detuned Radio]) == 0) && (my_meat() >= npc_price($item[Detuned Radio])) && auto_is_valid($item[Detuned Radio]))
	{
		auto_buyUpTo(1, $item[Detuned Radio]);
		auto_setMCDToCap();
	}

	if((my_adventures() <= 3) && (my_daycount() == 1) && in_hardcore())
	{
		if(LX_meatMaid())
		{
			return true;
		}
	}

	#Can we have some other way to check that we have AT skills? Checking all skills just to be sure.
	if((item_amount($item[Antique Accordion]) == 0) && (item_amount($item[Aerogel Accordion]) == 0) && isUnclePAvailable() && (my_meat() >= npc_price($item[Antique Accordion])) && (auto_predictAccordionTurns() < 10) && !in_glover())
	{
		boolean buyAntiqueAccordion = false;

		foreach SongCheck in ATSongList()
		{
			if(have_skill(SongCheck.to_skill()))
			{
				buyAntiqueAccordion = true;
			}
		}

		if(buyAntiqueAccordion)
		{
			auto_buyUpTo(1, $item[Antique Accordion]);
		}
	}

	if(item_amount($item[Seal Tooth]) == 0 && auto_is_valid($item[Seal Tooth]))
	{
		//saucerors want to use sealtooth to delay so that mortar shell delivers final blow for weaksauce MP explosion
		//TODO: add delaying for mortar for other classes in combat and then remove the sauceror requirement here.
		if(my_meat() > 7500 || (my_class() == $class[Sauceror] && canUse($skill[Stuffed Mortar Shell])))
		{
			acquireHermitItem($item[Seal Tooth]);
		}
	}
	

	if(my_class() == $class[Turtle Tamer] && !in_wotsf()) // no need for shields in way of the surprising fist
	{
		if(!possessEquipment($item[Turtle Wax Shield]) && (item_amount($item[Turtle Wax]) > 0))
		{
			use(1, $item[Turtle Wax]);
		}
		if(have_skill($skill[Armorcraftiness]) && !possessEquipment($item[Painted Shield]) && (my_meat() > 3500) && (item_amount($item[Painted Turtle]) > 0) && (item_amount($item[Tenderizing Hammer]) > 0))
		{
			// Make Painted Shield - Requires an Adventure
		}
		if(have_skill($skill[Armorcraftiness]) && !possessEquipment($item[Spiky Turtle Shield]) && (my_meat() > 3500) && (item_amount($item[Hedgeturtle]) > 1) && (item_amount($item[Tenderizing Hammer]) > 0))
		{
			// Make Spiky Turtle Shield - Requires an Adventure
		}
	}
	if((get_power(equipped_item($slot[pants])) < 70) && !possessEquipment($item[Demonskin Trousers]) && (my_meat() >= npc_price($item[Pants Kit])) && (item_amount($item[Demon Skin]) > 0) && (item_amount($item[Tenderizing Hammer]) > 0) && knoll_available())
	{
		auto_buyUpTo(1, $item[Pants Kit]);
		autoCraft("smith", 1, $item[Pants Kit], $item[Demon Skin]);
	}
	if(!possessEquipment($item[Tighty Whiteys]) && (my_meat() >= npc_price($item[Pants Kit])) && (item_amount($item[White Snake Skin]) > 0) && (item_amount($item[Tenderizing Hammer]) > 0) && knoll_available())
	{
		auto_buyUpTo(1, $item[Pants Kit]);
		autoCraft("smith", 1, $item[Pants Kit], $item[White Snake Skin]);
	}

	if(!possessEquipment($item[Grumpy Old Man Charrrm Bracelet]) && (item_amount($item[Jolly Roger Charrrm Bracelet]) > 0) && (item_amount($item[Grumpy Old Man Charrrm]) > 0))
	{
		use(1, $item[Jolly Roger Charrrm Bracelet]);
		use(1, $item[Grumpy Old Man Charrrm]);
	}

	if(!possessEquipment($item[Booty Chest Charrrm Bracelet]) && (item_amount($item[Jolly Roger Charrrm Bracelet]) > 0) && (item_amount($item[Booty Chest Charrrm]) > 0))
	{
		use(1, $item[Jolly Roger Charrrm Bracelet]);
		use(1, $item[Booty Chest Charrrm]);
	}

	if((item_amount($item[Magical Baguette]) > 0) && !possessEquipment($item[Loafers]))
	{
		visit_url("inv_use.php?pwd=&which=1&whichitem=8167");
		run_choice(2);
	}
	if((item_amount($item[Magical Baguette]) > 0) && !possessEquipment($item[Bread Basket]))
	{
		visit_url("inv_use.php?pwd=&which=1&whichitem=8167");
		run_choice(3);
	}
	if((item_amount($item[Magical Baguette]) > 0) && !possessEquipment($item[Breadwand]))
	{
		visit_url("inv_use.php?pwd=&which=1&whichitem=8167");
		run_choice(1);
	}

	if(knoll_available() && (have_skill($skill[Torso Awareness]) || have_skill($skill[Best Dressed])) && (item_amount($item[Demon Skin]) > 0) && !possessEquipment($item[Demonskin Jacket]))
	{
		//Demonskin Jacket, requires an adventure, knoll available doesn\'t matter here...
	}

	if(in_koe())
	{
		if((creatable_amount($item[Antique Accordion]) > 0 && !possessEquipment($item[Antique Accordion])) && auto_predictAccordionTurns() < 10)
		{
			retrieve_item(1, $item[Antique Accordion]);
		}
		if(creatable_amount($item[low-pressure oxygen tank]) > 0 && !possessEquipment($item[low-pressure oxygen tank]))
		{
			retrieve_item(1, $item[low-pressure oxygen tank]);
		}
	}

	LX_dolphinKingMap();
	auto_mayoItems();
	auto_checkTakerSpace();

	if(item_amount($item[Metal Meteoroid]) > 0 && !in_tcrs())
	{
		item it = $item[Meteorthopedic Shoes];
		if(!possessEquipment(it))
		{
			int choice = 1 + to_int(it) - to_int($item[Meteortarboard]);
			string temp = visit_url("inv_use.php?pwd=&which=3&whichitem=9516");
			temp = visit_url("choice.php?pwd=&whichchoice=1264&option=" + choice);
		}

		it = $item[Meteortarboard];
		if(!possessEquipment(it) && (get_power(equipped_item($slot[Hat])) < 140) && (get_property("auto_beatenUpCount").to_int() >= 5))
		{
			int choice = 1 + to_int(it) - to_int($item[Meteortarboard]);
			string temp = visit_url("inv_use.php?pwd=&which=3&whichitem=9516");
			temp = visit_url("choice.php?pwd=&whichchoice=1264&option=" + choice);
		}

		it = $item[Meteorite Guard];
		if(!possessEquipment(it) && !possessEquipment($item[Kol Con 13 Snowglobe]) && (get_property("auto_beatenUpCount").to_int() >= 5))
		{
			int choice = 1 + to_int(it) - to_int($item[Meteortarboard]);
			string temp = visit_url("inv_use.php?pwd=&which=3&whichitem=9516");
			temp = visit_url("choice.php?pwd=&whichchoice=1264&option=" + choice);
		}
	}

	{
		if(item_amount($item[Portable Pantogram]) > 0)
		{
			switch(my_daycount())
			{
			case 1:
				pantogramPants(my_primestat(), $element[hot], 1, 1, 1);
				break;
			default:
				pantogramPants(my_primestat(), $element[cold], 1, 2, 1);
				break;
			}
		}
		#set_property("_dailyCreates", true);
	}

	return false;
}
